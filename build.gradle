plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'com.chanwr'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // Include only the specified JARs without transitive dependencies
    implementation('org.apache.flink:flink-connector-kafka:3.3.0-1.20') {
        transitive = false
    }
    implementation('org.apache.kafka:kafka-clients:3.4.0') {
        transitive = false
    }
    implementation('org.apache.flink:flink-avro:1.20.2') {
        transitive = false
    }
    implementation('org.apache.avro:avro:1.11.4')
    implementation('org.apache.iceberg:iceberg-flink-runtime-1.20:1.10.0')
    // https://github.com/apache/iceberg/issues/14118
    implementation('org.apache.hadoop:hadoop-common:3.4.1') {
        // runs into java.lang.LinkageError: loader constraint violation: loader org.apache.flink.util.ChildFirstClassLoader @53f48368 wants to load class org.apache.commons.cli.Options. A different class with the same name was previously loaded by 'app'. (org.apache.commons.cli.Options is in unnamed module of loader 'app')
        exclude group: 'commons-cli', module: 'commons-cli'
    }
    // last version that has org/apache/hadoop/hdfs/HdfsConfiguration
    implementation('org.apache.hadoop:hadoop-hdfs:2.7.7') {
        transitive = false
    }
    // causes org.apache.tools.zip.Zip64RequiredException: archive contains more than 65535 entries.
    // implementation('software.amazon.awssdk:bundle:2.35.9')
    implementation('software.amazon.awssdk:s3:2.35.9')
    implementation('software.amazon.awssdk:auth:2.35.9')
    implementation('software.amazon.awssdk:core:2.35.9')
    implementation('software.amazon.awssdk:kms:2.35.9')
    implementation('software.amazon.awssdk:sts:2.35.9')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 11 // or your target Java version
}

tasks.shadowJar {
    archiveClassifier.set('uber')
    mergeServiceFiles() // handles META-INF/services/* conflicts
    // Exclude problematic META-INF files
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}